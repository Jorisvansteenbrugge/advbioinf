#!/usr/bin/env python

configfile: "config.yaml"
print(config["samples"])

rule all:
    input:
        "calls/all.vcf"

rule bwa_map:
    input:
        "data/genome.fa",
        "{sample}_1.fastq",
        "{sample}_2.fastq"
    output:
        "mapped_reads/{sample}.bam"
    threads:
        4
    shell:
        "bwa mem -t {threads} {input} | samtools view -Sb - > {output}"

rule samtools_sort:
    input:
        "mapped_reads/{sample}.bam"
    output:
        "sorted_reads/{sample}.bam"
    threads:
        1
    shell:
        "samtools sort -T sorted_reads/{wildcards.sample} "
        "-O bam {input} > {output}"

rule samtools_index:
    input:
        "sorted_reads/{sample}.bam"
    output:
        "sorted_reads/{sample}.bam.bai"
    threads:
        1
    shell:
        "samtools index {input}"

rule bcftools_call:
    input:
        fa = "data/genome.fa",
        bam = expand("sorted_reads/{sample}.bam", sample=config["samples"]),
        bai = expand("sorted_reads/{sample}.bam.bai", sample=config["samples"])
    output:
        "calls/all.vcf"
    threads:
        1
    shell:
        "samtools mpileup -g -f {input.fa} {input.bam} | "
        "bcftools call -mv - > {output}"
